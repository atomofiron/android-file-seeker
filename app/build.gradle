apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'appmetrica.gradle'

android {
    def fileProviderName = ".FileProvider"
    def appId = "app.atomofiron.searchboxapp"

    compileSdkVersion 32

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId appId
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 4
        versionName "1.0.1"

        resValue "string", "version_name", "v$versionName ($versionCode)"
    }
    signingConfigs {
        beta {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        debug {
            resValue "string", "app_name", "Search Box D"
            applicationIdSuffix ".debug"
            manifestPlaceholders += [PROVIDER: appId + applicationIdSuffix + fileProviderName]
            buildConfigField "String", "AUTHORITY", "\"${appId + applicationIdSuffix + fileProviderName}\""
            buildConfigField "String", "YANDEX_API_KEY", "null"
        }
        beta {
            resValue "string", "app_name", "Search Box B"
            signingConfig signingConfigs.beta
            applicationIdSuffix ".beta"
            manifestPlaceholders += [PROVIDER: appId + applicationIdSuffix + fileProviderName]
            buildConfigField "String", "AUTHORITY", "\"${appId + applicationIdSuffix + fileProviderName}\""
            buildConfigField "String", "YANDEX_API_KEY", "\"$yandexApiKey\""
        }
        release {
            resValue "string", "app_name", "Search Box App"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders += [PROVIDER: appId + applicationIdSuffix + fileProviderName]
            buildConfigField "String", "YANDEX_API_KEY", "\"$yandexApiKey\""
        }
    }
    lintOptions {
        checkReleaseBuilds false
        //abortOnError false
    }
}

ext {
    androidx = "1.7.0"
    fragment = "1.4.1"
    material = "1.6.0"
    appcompat = "1.4.1"
    coroutines = "1.6.1"
    lifecycle_ext = "2.2.0"
    lifecycle_ktx = "2.4.1"
    retrofit = "2.6.0"
    constraint = "2.1.4"
    preference = "1.2.0"
    flexbox = "2.0.1"
    room = "2.4.2"
    dagger = "2.39.1"
    leakcanary = "2.2"
    metrica = "3.13.1"
    work = "2.7.1"
    play_core = "1.10.3"
    play_core_ktx = "1.8.1"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.preference:preference-ktx:$preference"
    implementation "androidx.core:core-ktx:$androidx"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_ext"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_ktx"
    implementation "androidx.fragment:fragment-ktx:$fragment"
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$constraint"
    implementation "androidx.work:work-runtime-ktx:$work"

    implementation "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$room"

    implementation "com.google.android.material:material:$material"
    implementation "com.google.android:flexbox:$flexbox"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"

    implementation "com.yandex.android:mobmetricalib:$metrica"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary"
    betaImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary"

    implementation "com.google.android.play:core:$play_core"
    implementation "com.google.android.play:core-ktx:$play_core_ktx"
}

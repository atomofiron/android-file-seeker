apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    def fileProviderName = ".FileProvider"
    def appId = "app.atomofiron.searchboxapp"

    compileSdk 33

    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId appId
        minSdk 21
        targetSdk 32
        versionCode 8
        versionName "1.1.2"

        resValue "string", "version_name", "v$versionName ($versionCode)"
    }
    signingConfigs {
        beta {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        debug {
            resValue "string", "app_name", "File Portal D"
            applicationIdSuffix ".debug"
            manifestPlaceholders += [PROVIDER: appId + applicationIdSuffix + fileProviderName]
            buildConfigField "String", "AUTHORITY", "\"${appId + applicationIdSuffix + fileProviderName}\""
        }
        beta {
            resValue "string", "app_name", "File Portal B"
            signingConfig signingConfigs.beta
            applicationIdSuffix ".beta"
            manifestPlaceholders += [PROVIDER: appId + applicationIdSuffix + fileProviderName]
            buildConfigField "String", "AUTHORITY", "\"${appId + applicationIdSuffix + fileProviderName}\""
        }
        release {
            resValue "string", "app_name", "File Portal"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders += [PROVIDER: appId + applicationIdSuffix + fileProviderName]
        }
    }
    lint {
        checkReleaseBuilds false
    }
    namespace 'app.atomofiron.searchboxapp'
}

repositories {
    maven { url "https://jitpack.io" }
}

ext {
    androidx = "1.9.0"
    recyclerview = "1.3.0"
    fragment = "1.5.5"
    material = "1.8.0"
    appcompat = "1.6.1"
    coroutines = "1.6.4"
    lifecycle_ext = "2.2.0"
    lifecycle_ktx = "2.5.1"
    retrofit = "2.6.0"
    constraint = "2.1.4"
    navigation = "2.5.3"
    preference = "1.2.0"
    flexbox = "2.0.1"
    room = "2.5.0"
    dagger = "2.44.2"
    leakcanary = "2.9.1"
    work = "2.8.0"
    play_core = "1.10.3"
    play_core_ktx = "1.8.1"
    datastore = "1.1.0-alpha01"
    exif = "1.3.6"
    async_inflater = "1.0.0"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.preference:preference-ktx:$preference"
    implementation "androidx.core:core-ktx:$androidx"
    implementation "androidx.recyclerview:recyclerview:$recyclerview"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_ext"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_ktx"
    implementation "androidx.fragment:fragment-ktx:$fragment"
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$constraint"
    implementation "androidx.work:work-runtime-ktx:$work"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    implementation "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$room"

    implementation "com.google.android.material:material:$material"
    implementation "com.google.android:flexbox:$flexbox"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary"
    betaImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary"

    implementation "com.google.android.play:core:$play_core"
    implementation "com.google.android.play:core-ktx:$play_core_ktx"

    implementation "com.github.atomofiron:android-window-insets-compat:1.1.0"
    implementation "androidx.datastore:datastore-preferences:$datastore"
    implementation "androidx.exifinterface:exifinterface:$exif"
    implementation "androidx.asynclayoutinflater:asynclayoutinflater:$async_inflater"
}
